apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.noveogroup.android.check'
apply plugin: 'com.getkeepsafe.dexcount'

def version = [major: 0, minor: 1, patch: 0] // see: http://semver.org
def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.onebitmedia.awesomeapp"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode version.major * 10000 + version.minor * 1000 + version.patch * 100
        versionName "${version.major}.${version.minor}.${version.patch}"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        staging {
        }

        production {
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

check {
    abortOnError true
    checkstyle {
        config getRootProject().file('config/checkstyle.xml')
    }
    pmd {
        config getRootProject().file('config/pmd.xml')
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-annotations:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'nl.littlerobots.squadleader:squadleader:1.2'
}
